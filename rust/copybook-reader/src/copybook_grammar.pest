whitespace = _{ " " }

picture = _{
    // In Cobol Picture clauses are used to define the attributes of the data type.
    // This is a silent rule because although it identifies a data type clause it does
    // not provide actual value to the CopybookDefinition structure.
    "PIC"
}

level = { 
    ASCII_DIGIT+ 
}

integer = {
    ASCII_DIGIT+
}

label = {
    // labels must start with at least one letter or number but they can be followed by
    // any combination of letters, numbers, or dashes
    ASCII_ALPHANUMERIC 
    ~ (
        ASCII_ALPHANUMERIC
        | "-"
      )*
}

data_type = {
    picture
    ~ whitespace+
    ~ (
        alphanumeric_type
        | numeric_type
    )
}

alphanumeric_type = {
    "X"
    ~ (
        // Supports alpha-numeric data type format X(100)
        "(" ~ integer ~ ")"
        // Supports alpha-numeric data type format XXXXX
        | ("X"*)
    )
}

numeric_type = {
    "9"
    ~ (
        // Supports the numeric data type format 9(100)
        ("(" ~ ASCII_DIGIT+ ~ ")")
        // Supports the numeric data type format 9999
        | ( "9"* )
    )
}

field = {
    label ~ whitespace+ ~ data_type
}

group = {
    label
}

statement = {
    whitespace*
    ~ level
    ~ whitespace+
    ~ (field | group)
    ~ "."
}

file = {
    SOI
    ~ (
        statement
        ~ NEWLINE?
      )+
    ~ whitespace* // Some copybooks may have trailing whitespace
    ~ EOI
}